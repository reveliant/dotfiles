#!/usr/bin/awk -f
BEGIN { 
	printf "["
	first = 1
	tab="    "
	newline="\n"
}

/IN=/ {
	if (!first) {
		printf ","
	} else {
		first = 0
	}

	if (ENVIRON["human"] || ENVIRON["readable"]) printf newline

	if (ENVIRON["human"]) printf tab
	printf "{"
	if (ENVIRON["human"]) printf newline
	
	for (element=1; element <= NF; element++) {
		if (match($element, /^([A-Z]+)(=|$)/, data)) {
			if (ENVIRON["human"]) printf tab tab
			printf "\"%s\":", tolower(data[1])
			if (ENVIRON["human"]) printf " "

			if (match($element, /^[A-Z]+=([0-9]+)$/, attr)) {
				printf "%i", attr[1]
			} else {
				if (match($element, /^[A-Z]+=(.*)$/, attr)) {
					printf "\"%s\"", attr[1]
				} else {
					printf "true"
				}
			}

			if (element < NF) printf ","
			if (ENVIRON["human"]) printf newline
			if (ENVIRON["readable"]) printf " "
		} else {
			if (match($element, /^[0-9]{4}-[0-9]{2}-[0-9]{2}(T| )([01][0-9]|2[0-4]):[0-5][0-9]:[0-5][0-9](+(0[0-9]|1[0-2])[0-5]0)?$/)) {
				if (ENVIRON["human"]) printf tab tab
				printf "\"timestamp\":"
				if (ENVIRON["human"]) printf " "
				printf "\"%s\"", $element
				if (element < NF) printf ","
				if (ENVIRON["human"]) printf newline
				if (ENVIRON["readable"]) printf " "
			}
		}
	}

	if (ENVIRON["human"]) printf tab
	printf "}"
}

END {
	if (ENVIRON["human"] || ENVIRON["readable"]) printf newline
	printf "]" newline
}
